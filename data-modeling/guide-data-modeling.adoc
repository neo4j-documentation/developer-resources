= Graph Data Modeling Guidelines
:level: Intermediate
:toc:
:toc-placement!:
:toc-title: Overview
:toclevels: 2
:section: Graph Data Modeling

.Goals
[abstract]
This guide is designed to walk you through the graph data modeling lifecycle of Neo4j. You will be introduced to the basic process of designing a graph data model that can answer a wide range of business questions across a variety of domains. 

.Prerequisites
[abstract]
You should have a firm understanding of the Neo4j's link:./property-graph[property graph data model], and its query language, link:./cypher[Cypher].

[role=expertise]
{level}

toc::[]

:img: .

== Introduction

Graph data modeling is the process in which a Neo4j user describes an arbitrary domain as a connected graph of nodes and relationships. From this description, a graph data model is designed to answer questions in the form of Cypher queries.

== Describing a domain

Consider the following description of the connection between two people, Sally and John.

[quote]
Two _people_, *John* and *Sally*, are +++<u>friends</u>+++. Both *John* and *Sally* have +++<u>read</u>+++ the _book_, *Graph Databases*.

We can take this statement, identify components such as labels, nodes, and relationships, and use these to build our model. 

=== Nodes

The first entity that we'll identify in our domain are the nodes. 

[quote, "3.1 Nodes", "docs.neo4j.org"]
The fundamental units that form a graph are nodes and relationships. In Neo4j, both nodes and relationships can contain properties. Nodes are often used to represent entities, but depending on the domain relationships may be used for that purpose as well. Apart from properties and relationships, nodes can also be labeled with zero or more labels.

image::{img}node.svg[Nodes]

We can identify nodes as entities with a unique conceptual identity. In this case, these entities are outlined below in bold.

[quote]
Two people, *John* and *Sally*, are friends. Both *John* and *Sally* have read the book, *Graph Databases*.

Extracting the nodes:

* *John*
* *Sally*
* *Graph Databases*

image::{img}data-modeling-3.png[Extract Nodes]

[role=side-nav]
=== Recommended

[role=recommended]
* *Neo4j Docs:*
  * http://docs.neo4j.org/chunked/stable/graphdb-neo4j-nodes.html[3.1. Nodes]

=== Labels

Now that we have an idea of what our nodes will be, we decide what labels (if any) to assign our nodes. 

[quote, "3.4 Labels", "docs.neo4j.org"]
A label is a named graph construct that is used to group nodes into sets. All nodes labeled with the same label belongs to the same set. Many database queries can work with these sets instead of the whole graph, making queries easier to write and more efficient. A node may be labeled with any number of labels, including none, making labels an optional addition to the graph.

image::{img}label.svg[Labels]

First we'll start by identifying the roles of objects (John, Sally, Graph Databases) mentioned in the statement. We know that this statement is about two different types of objects:

[quote]
Two _people_, John and Sally, are friends. Both John and Sally have read the _book_, Graph Databases.

Extracting the labels:

* _Person_
* _Book_

image::{img}data-modeling-2.png[Extract labels]

Now that we have identified both our nodes and labels, we can assign the labels to the nodes they describe. For *John* and *Sally* we apply the role _Person_. For *Graph Databases* we apply the role _Book_.

image::{img}data-modeling-4.png[Apply labels]

[role=side-nav]
=== Recommended

[role=recommended]
* *Neo4j Docs:*
  * http://docs.neo4j.org/chunked/stable/graphdb-neo4j-labels.html[3.4. Labels]

=== Relationships

Further, we can identify the interactions between these subjects:

* *John* is a +++<u>friend</u>+++ of *Sally*
* *Sally* is a +++<u>friend</u>+++ of *John*
* *John* has +++<u>read</u>+++ *Graph Databases*
* *Sally* has +++<u>read</u>+++ *Graph Databases*

Now that we have described the kinds of things in our domain as nodes with their labels, we can connect the nodes together to describe their interactions.

Pairs of nodes labeled _Person_ can be connected to each other by the +++<u>friend of</u>+++ relationship.

Pairs of nodes labeled _Book_ and _Person_ can be connected to each other by a +++<u>has read</u>+++ relationship.

=== Draw the data model

Now that we have identified the kinds of relationships that can exist between labels of nodes, we can complete our graph data model.

image::{img}data-modeling-1.png[Graph data model]

== Answering questions

We have gone through the process of creating a basic graph data model for the interactions between people and books. We can take this data model further by defining attributes of these entities as key-value properties.

=== List your questions

First, start by listing your questions that you want to answer about your data.

* When did John and Sally become friends?
* What is the average rating of the book Graph Databases?
* Who is the author of the book Graph Databases?
* How old is Sally?
* How old is John?
* Who is older, Sally or John?
* Who read the book _Graph Databases_ first, Sally or John?

From these list of questions, you can identify the attributes that must belong to entities within your data model.

image:data-modeling-5.png[Graph data model with properties]

[role=side-nav]
=== Recommended

[role=recommended]
* http://docs.neo4j.org/chunked/stable/data-modeling-examples.html[Data Modeling Examples]
* https://github.com/neo4j-contrib/graphgist/wiki[GraphGist Project]
* http://vimeo.com/89075703[Video]

=== Create a sample dataset

Now that we have a complete graph data model for our domain that sufficiently answers our questions, we can go about creating a sample dataset using Cypher.

[source,cypher]
----
// Create Sally
CREATE (sally:Person { name: 'Sally', age: 29 })

// Create John
CREATE (john:Person { name: 'John', age: 27 })

// Create Graph Databases book
CREATE (gdb:Book { title: 'Graph Databases',
                   authors: ['Ian Robinson', 'Jim Webber'] })

// Connect Sally and John as friends
CREATE (sally)-[:FRIEND_OF { since: 1357718400 }]->(john)

// Connect Sally to Graph Databases book
CREATE (sally)-[:HAS_READ { rating: 4, on: 1360396800 }]->(gdb)

// Connect John to Graph Databases book
CREATE (john)-[:HAS_READ { rating: 5, on: 1359878400 }]->(gdb)
----

=== Translate questions to Cypher queries

Now that we have a sample dataset of our graph data model, we can translate our questions from earlier into queries.

==== When did John and Sally become friends?

[source,cypher]
----
MATCH (sally:Person { name: 'Sally' })
MATCH (john:Person { name: 'John' })
MATCH (sally)-[r:FRIEND_OF]-(john)
RETURN r.since as friends_since
----

==== What is the average rating of _Graph Databases_?

[source,cypher]
----
MATCH (gdb:Book { title: 'Graph Databases' })
MATCH (gdb)<-[r:HAS_READ]-()
RETURN avg(r.rating) as average_rating
----

==== Who are the authors of _Graph Databases_?

[source,cypher]
----
MATCH (gdb:Book { title: 'Graph Databases' })
RETURN gdb.authors as authors
----

==== How old is Sally?

[source,cypher]
----
MATCH (sally:Person { name: 'Sally' })
RETURN sally.age as sally_age
----

==== How old is John?

[source,cypher]
----
MATCH (john:Person { name: 'John' })
RETURN john.age as john_age
----

==== Who is older, Sally or John?

[source,cypher]
----
MATCH (people:Person)
WHERE people.name = 'John' OR people.name = 'Sally'
RETURN people.name as oldest
ORDER BY people.age DESC
LIMIT 1
----

==== Who read _Graph Databases_ first, Sally or John?

[source,cypher]
----
MATCH (people:Person)
WHERE people.name = 'John' OR people.name = 'Sally'
MATCH (people)-[r:HAS_READ]->(gdb:Book { title: 'Graph Databases' })
RETURN people.name as first_reader
ORDER BY r.on
LIMIT 1
----
