= From SQL to Cypher - A hands-on Guide
:slug: guide-sql-to-cypher
:level: Beginner
:section: Cypher Query Language
:section-link: cypher
:sectanchors:
:toc:
:toc-title: Contents
:toclevels: 1

.Goals
[abstract]
This guide teaches anyone familiar with SQL how to write the equivalent, efficient Cypher statements.
We'll use the well-known Northwind database to explain the concepts and work through the queries from simple to advanced.

.Prerequisites
[abstract]
You should have a basic understanding of the link:/developer/get-started/graph-database#property-graph[property graph model].
Having link:/download[downloaded] and installed Neo4j helps you code along with the examples.
Importing the data is covered in our link:/developer/working-with-data/guide-importing-data-and-etl[data import guide].

[role=expertise]
{level}

[#cypher-for-sql-dev]
== A few words about Cypher

Cypher is like SQL a declarative, textual query language, but for graphs.

It consists of clauses, keywords and expressions like predicates and functions, many of which will be familiar (like `WHERE`, `ORDER BY`, `SKIP LIMIT`, `AND`, `p.unitPrice > 10`).

Unlike SQL, Cypher is all about expressing graph patterns.
We added a special clause `MATCH` for *matching* those patterns in your data.
These patterns are what you would usually draw on a whiteboard, just converted into text using *ASCII-art symbols*.

We represent the circles of node entities with round parentheses, like this: `(p:Product)`.

And arrows of relationships are drawn as such `+-->+`, you can add relationship-type and other information in square brackets +
`+-[:ORDERED]->+`.

Bringing both together `+()-->()<--()+` looks almost like our original diagram.
It gives us a first hint at the expressiveness of graph patterns: `+(cust:Customer)-[:ISSUED]->(o:Order)-[:CONTAINS]->(prod:Product)+`.

// what is going on here?
Other highlights of Cypher are graph concepts like paths, variable length paths, shortest-path functions;
the support of many functions, operations and predicates on lists and the ability to chain query parts.

You can use Cypher to update the graph structure and data and even ingest large amounts of CSV data.

With *user defined procedures* you can extend the language with functionality that you need but which is currently not yet available.

The full Cypher language documentation is available in the {cyphermanual}/[Neo4j Cypher Manual^] together with a complete link:/docs/cypher-refcard/current/[Reference Card].

With the http://openCypher.org[openCypher project], Cypher became an open effort for a modern graph query language, that is supported by several database companies.
The openCypher project also provides syntax diagrams that you're used to from SQL:

++++
<object type="image/svg+xml" data="//s3.amazonaws.com/artifacts.opencypher.org/railroad/Pattern.svg"></object>
++++

[#cypher-sql-data-model]
== Data Model Transformation

Relational databases store data in tables with fixed structure (schema), each column having a name, type, length, constraints etc.
References between tables are represented by repeating the primary key of one table as column in another as a foreign key.
For many-to-many references, JOIN-tables (or link-tables) are needed as artificial construct between the connected tables.

A well normalized relational model can be directly translated into a equivalent graph model.
Graph models are mostly driven by use-cases, so there will be opportunity for optimization and model evolution afterwards.

A good, well normalized Entity-Relationship diagram often already represents a decent graph model.
So if you still have the original ER diagram of your database available, try to use it as a guide.

For such a sensible relational model, the transformation is not too hard.
Rows of entity tables are converted into nodes and foreign-key relationships and JOIN tables into relationships.

It is important to have an good understanding of the graph model before you start to import data, then it just becomes the task of hydrating that model.

[#cypher-sql-data-import]
== Data Import

Most relational databases allow an easy export of tables to CSV files, e.g. in Postgres `COPY (SELECT * FROM customers) TO '/tmp/customers.csv' WITH CSV header;`.
Those files can come from single tables, but might also represent a set of joined tables with some duplicate data.

They can be imported by using Cypher's `LOAD CSV` capability, which we explain in detail in these guides:

* link:/developer/working-with-data/guide-importing-data-and-etl["Guide: Data Import",role=guide]
* http://watch.neo4j.org/video/112447027["Webinar: Data Import",role=video]
* link:/developer/working-with-data/guide-import-csv["Guide CSV Import",role=guide]

If you are a developer you can also just connect to the relational database with your regular driver, load the data from there with SQL.
And directly create the graph structure by using a Neo4j driver for your stack to send the equivalent, parametrized Cypher update statements to Neo4j.

[#cypher-sql-patterns]
== Cypher is all about Patterns

As already mentioned, the essence of your Cypher statements are the patterns you are are interested in.

In a node pattern `(variable:Label)` you can use a variable and one or more labels for the node.
You can also provide attributes as key-value structure, e.g. (item:Product {name:"Chocolade"}).

For relationship-patterns like `()-[someRel:REL_TYPE]->()` it is the same, only that you might chose an variable like `someRel` and one or more alternative relationship-types.

Like with SQL aliases you can use the variables later to refer to the nodes and relationships they represent, e.g. to access their properties or call a function on them.

Patterns are used both for querying as well as updating the graph structures.

They are usually used in the `MATCH` clause, but can also be treated as expressions or predicates.
That's especially helpful when making sure that certain patterns shouldn't exist.

[#cypher-sql-northwind]
== Northwind Example Model

The well known Northwind database represents the data storage of a retail application.
You'll find customers, products, orders, employees, shippers and categories as well as their interactions.

Please refer to the relational and graph model below when considering the data structures in the following queries.

=== Relational model

image::{img}Northwind_diagram.jpg[]

=== Graph Model

image::{img}northwind_graph_simple.png[width=800]

[#cypher-sql-queries]
== Querying the Data Step by Step

The intent of this guide is to introduce Cypher by comparing it with the equivalent SQL statements, so that your existing SQL knowledge allows your to understand it immediately.

=== Find all Products

_Select and Return Records_

Easy in SQL, just select everything from the `products` table.

[source, plsql]
----
SELECT p.*
FROM products as p;
----

//sqltable

Similarly in Cypher, you just *match* a simple pattern: all nodes with the *label* `:Product` and `RETURN` them.

[source, cypher]
----
MATCH (p:Product)
RETURN p;
----

//table

_Field Access, Ordering and Paging_

*More efficient is to return only a subset of attributes*, like `ProductName` and `UnitPrice`.
And while we're on it, let's also order by price and only return the 10 most expensive items.

[source, plsql]
----
SELECT p.ProductName, p.UnitPrice
FROM products as p
ORDER BY p.UnitPrice DESC
LIMIT 10;
----

//sqltable

You can copy and paste the changes from SQL to Cypher, it's thankfully unsurprising.
But remember that labels, relationship-types and property-names are *case sensitive* in Neo4j.

[source, cypher]
----
MATCH (p:Product)
RETURN p.productName, p.unitPrice
ORDER BY p.unitPrice DESC
LIMIT 10;
----

// table

=== Find single Product by Name

_Filter by Equality_

If we only want to look at a single Product, for instance delicious _Chocolade_, we filter in SQL with a `WHERE` clause.

[source, plsql]
----
SELECT p.ProductName, p.UnitPrice
FROM products AS p
WHERE p.ProductName = 'Chocolade';
----

//sqltable

Same in Cypher, here the `WHERE` belongs to the `MATCH` statement. Boring.

[source, cypher]
----
MATCH (p:Product)
WHERE p.productName = "Chocolade"
RETURN p.productName, p.unitPrice;
----

// table

There is a shortcut in Cypher if you match for a labeled node with a certain attribute.

[source, cypher]
----
MATCH (p:Product {productName:"Chocolade"})
RETURN p.productName, p.unitPrice;
----

_Indexing_

// table

If you want to match quickly by this node-label and attribute combination, it makes sense to create an index for that pair, if you haven't during the link:/developer/working-with-data/guide-importing-data-and-etl[import].

[source, cypher]
----
CREATE INDEX ON :Product(productName);
CREATE INDEX ON :Product(unitPrice);
----

=== Filter Products

_Filter by List/Range_

You can also filter by multiple values.

[source, plsql]
----
SELECT p.ProductName, p.UnitPrice
FROM products as p
WHERE p.ProductName IN ('Chocolade','Chai');
----

//sqltable

Similiarly in Cypher which has full collection support, not just the `IN` operator but collection functions, predicates and transformations.

[source, cypher]
----
MATCH (p:Product)
WHERE p.productName IN ['Chocolade','Chai']
RETURN p.productName, p.unitPrice;
----

// table

_Filter by Multiple Numeric and Textual Predicates_

Filtering can go further, let's try to find some expensive things starting with "C".

[source, plsql]
----
SELECT p.ProductName, p.UnitPrice
FROM products AS p
WHERE p.ProductName LIKE 'C%' AND p.UnitPrice > 100;
----

//sqltable

The `LIKE` operator is replaced by a `STARTS WITH` (there are also `CONTAINS` and `ENDS WITH`) all three of which are index supported.

[source, cypher]
----
MATCH (p:Product)
WHERE p.productName STARTS WITH "C" AND p.unitPrice > 100
RETURN p.productName, p.unitPrice;
----

You can also use a regular expression, like `p.productName =~ "C.*"`.

// table

=== Joining Products with Customers

_Join Records, Distinct Results_

We want to see who bought _Chocolade_.
Let's join the four tables together, refer to the model (ER-diagram) when you're unsure.

[source, plsql]
----
SELECT DISTINCT c.CompanyName
FROM customers AS c
JOIN orders AS o ON (c.CustomerID = o.CustomerID)
JOIN order_details AS od ON (o.OrderID = od.OrderID)
JOIN products AS p ON (od.ProductID = p.ProductID)
WHERE p.ProductName = 'Chocolade';
----

//sqltable

The graph model (have a look) is much simpler, as we don't need join tables, and expressing connections as graph patterns, is easier to read too.

[source, cypher]
----
MATCH (p:Product {productName:"Chocolade"})<-[:PRODUCT]-(:Order)<-[:PURCHASED]-(c:Customer)
RETURN distinct c.companyName;
----

// table

=== New Customers without Orders yet

_Outer Joins, Aggregation_

If we turn the question around and ask "What have I bought and paid in total?", the JOIN stays the same, only the filter expression changes.
Except if we have customers without any orders and still want to return them.
Then we have to use OUTER joins to make sure that results are returned even if there were no matching rows in other tables.

[source, plsql]
----
SELECT p.ProductName, sum(od.UnitPrice * od.Quantity) AS Volume
FROM customers AS c
LEFT OUTER JOIN orders AS o ON (c.CustomerID = o.CustomerID)
LEFT OUTER JOIN order_details AS od ON (o.OrderID = od.OrderID)
LEFT OUTER JOIN products AS p ON (od.ProductID = p.ProductID)
WHERE c.CompanyName = 'Drachenblut Delikatessen'
GROUP BY p.ProductName
ORDER BY Volume DESC;
----

//sqltable

In our Cypher query, the MATCH between customer and order becomes an OPTIONAL MATCH, which is the equivalent of an OUTER JOIN.

[source, cypher]
----
MATCH (c:Customer {companyName:"Drachenblut Delikatessen"})
OPTIONAL MATCH (p:Product)<-[pu:PRODUCT]-(:Order)<-[:PURCHASED]-(c)
RETURN p.productName, toInt(sum(pu.unitPrice * pu.quantity)) AS volume
ORDER BY volume DESC;
----

// table

=== Top-Selling Employees

_Aggregation, Grouping_

In the previous query we sneaked in a bit of aggregation.
By summing up product prices and ordered quantities, we provided an aggregated view per product for this customer.

You can use aggregation functions like `sum, count, avg, max` both in SQL and Cypher.
In SQL, aggregation is explicit so you have to provide all grouping keys again in the `GROUP BY` clause.
If we want to see our top-selling employees.

[source, plsql]
----
SELECT e.EmployeeID, count(*) AS Count
FROM Employee AS e
JOIN Order AS o ON (o.EmployeeID = e.EmployeeID)
GROUP BY e.EmployeeID
ORDER BY Count DESC LIMIT 10;
----

//sqltable

In Cypher grouping for aggregation is implicit.
As soon as you use the first aggregation function, all non-aggregated columns automatically become grouping keys.

[source, cypher]
----
MATCH (:Order)<-[:SOLD]-(e:Employee)
RETURN e.name, count(*) AS cnt
ORDER BY cnt DESC LIMIT 10
----

//table

=== Employee Territories

_Collecting Master-Detail Queries_

In SQL there is a particularly dreaded kind of query - master detail information.
You have one main entity (master, head, parent) and many dependent ones (detail, position, child).
Usually you either query it by joining both and returning the master data multiple times (once for each detail), or by only fetching the primary key of the master and then pulling all detail rows via that foreign key.

For instance if we look at the employees per territory, then the territory information is returned for each employee.

[source, plsql]
----
SELECT e.LastName, et.Description
FROM Employee AS e
JOIN EmployeeTerritory AS et ON (et.EmployeeID = e.EmployeeID)
JOIN Territory AS t ON (et.TerritoryID = t.TerritoryID);
----

//sqltable

In Cypher we can either return the structure like in SQL.
Or we can choose to use the `collect` aggregation function, which aggregates values into a collection (list,array).
So we only return one row per parent, containing an inlined collection of child values.
This also works for nested values.

[source, cypher]
----
MATCH (t:Territory)<-[:IN_TERRITORY]-(e:Employee)
RETURN t.description, collect(e.lastName);
----

//table

=== Product Categories

_Hierarchies and Trees, Variable Length Joins_

If you have to express category-, territory- or organizational hierarchies in SQL then you model it usually with a self-join via a foreign key from child to parent.
Adding data is not problematic, as are single level queries (get all children for this parent).
As soon as you get into multi-level queries, the number of joins explodes, especially if your level depth not fixed.

Taking the example of the product categories, we have to decide upfront up to how many levels of categories we want to query.
We will tackle only three potential levels here (which means 1+2+3 = 6 self-joins of the ProductCategory table).

// TODO check

[source, plsql]
----
SELECT p.ProductName
FROM Product AS p
JOIN ProductCategory pc ON (p.CategoryID = pc.CategoryID AND pc.CategoryName = "Dairy Products")

JOIN ProductCategory pc1 ON (p.CategoryID = pc1.CategoryID
JOIN ProductCategory pc2 ON (pc2.ParentID = pc2.CategoryID AND pc2.CategoryName = "Dairy Products")

JOIN ProductCategory pc3 ON (p.CategoryID = pc3.CategoryID
JOIN ProductCategory pc4 ON (pc3.ParentID = pc4.CategoryID)
JOIN ProductCategory pc5 ON (pc4.ParentID = pc5.CategoryID AND pc5.CategoryName = "Dairy Products")
;
----

//sqltable

Cypher is able to express hierarchies of any depth just with the appropriate relationships.
Variable levels are represented by variable length paths, which are denoted by a star `*` after the relationship-type and optional limits (`min..max`).

[source, cypher]
----
MATCH (p:Product)-[:CATEGORY]->(l:ProductCategory)-[:PARENT*0..]-(:ProductCategory {name:"Dairy Products"})
RETURN p.name
----

//table

There is much more to Cypher than shown in this short section.
Hopefully the comparison with SQL helped you to understand the concepts.
If you are intrigued by the possibilities and want to try and learn more, just link:/download[install Neo4j] on your machine and use the links to our different Cypher learning resources.

Learn Cypher:

* link:/developer/cypher/cypher-query-language[Cypher Intro Guide]
* link:/online-course[Interactive Online Course]
* {cyphermanual}[Cypher Manual^]
* link:/docs/cypher-refcard/current/[Reference Card]

////

MATCH (choc:Product {ProductName:'Chocolade'})<-[:PRODUCT]-(:Order)<-[:SOLD]-(employee),
      (employee)-[:SOLD]->()-[:PRODUCT]->(other:Product)
RETURN employee.EmployeeID, other.ProductName, count(*) as count
ORDER BY count DESC
LIMIT 5

also introduce order by, limit

==== Find all Products in Category X

discuss graph patterns with relationships

==== Hierarchy

==== Varlength path

==== With

==== Shortest Path

=== Create a new product

=== create an order

////

[#cypher-resources]
== Resources
* link:/developer/get-started/graph-db-vs-rdbms["Data Model Transformation",role=guide]
